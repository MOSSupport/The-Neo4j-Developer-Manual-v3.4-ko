### 3.2.10. 리스트 

```
Cypher는 리스트 목록을 종합적으로 지원합니다. 
```

- 일반 리스트
- 리스트 이해
- 패턴 이해

 
리스트 연결 (```+```), 요소 존재 확인(```IN```) 및 억세스(```[]```)와 같은 연산자는 [이곳](./operators.md)에서 확인할 수 있습니다. ```IN```의 사용 및 ```null```과 관련된 ```[]```연산자는 [이곳](./working-with-null.md)에서 확인할 수 있씁니다.  

#### 3.2.10.1. 일반 리스트

리터럴 리스트는 대괄호를 사용하고 리스트 요소를 쉼표로 분리하여 생성합니다.

**쿼리**
```
RETURN [0, 1, 2, 3, 4, 5, 6, 7, 8, 9] AS list
```

| 리스트                    |
| ----------------------- |
| 1 행                     |
| ```[0,1,2,3,4,5,6,7,8,9]``` |

다음 예에서, [`range`](/cypher/functions.md) 함수를 사용할 것 입니다. 리스트 내 주어진 시작 및 끝 숫자 사이의 모든 숫자를 제공합니다. 범위는 양 끝에 포함됩니다.

리스트 내 각 요소에 접근하기 위해서, 사각 괄호를 다시 사용합니다. 이렇게하면 시작 색인에서 끝 색인까지 포함하지만 끝 색인에서 제외됩니다.

**쿼리**

```
RETURN range(0, 10)[3]
```

| range(0, 10)[3] |
| --------------- |
| 1 row           |
| `3`             |

리스트의 끝 부분에서 시작할 때, 음수를 사용할 수도 있습니다. 

**쿼리**

```
RETURN range(0, 10)[-3]
```

| range(0, 10)[-3] |
| ---------------- |
| 1 row            |
| `8`              |

마지막으로, 리스트 범위를 반환하기 위해서 괄호 안의 범위를 사용할 수 있습니다. 

**쿼리** 

```
RETURN range(0, 10)[0..3]
```

| range(0, 10)[0..3] |
| ------------------ |
| 1 row              |
| `[0,1,2]`          |

**쿼리**

```
RETURN range(0, 10)[0..-5]
```

| range(0, 10)[0..-5] |
| ------------------- |
| 1 row               |
| `[0,1,2,3,4,5]`     |

**쿼리**

```
RETURN range(0, 10)[-5..]
```

| range(0, 10)[-5..] |
| ------------------ |
| 1 row              |
| `[6,7,8,9,10]`     |

**쿼리**

```
RETURN range(0, 10)[..4]
```

| range(0, 10)[..4] |
| ----------------- |
| 1 row             |
| `[0,1,2,3]`       |

범위를 넘은 슬라이스는 잘리지만 바인딩되지 않은 단일 요소는 ```null```을 반환합니다.

**쿼리**

```
RETURN range(0, 10)[15]
```

| range(0, 10)[15] |
| ---------------- |
| 1 row            |
| `<null>`         |

**쿼리**

```
RETURN range(0, 10)[5..15]
```

| range(0, 10)[5..15] |
| ------------------- |
| 1 row               |
| `[5,6,7,8,9,10]`    |

리스트의 [```size```](/cypher/functions.md)를 다음과 같이 얻을 수 있습니다:

**쿼리**

```
RETURN size(range(0, 10)[0..3])
```

| size(range(0, 10)[0..3]) |
| ------------------------ |
| 1 row                    |
| `3`                      |

#### 3.2.10.2. 리스트 이해

리스트 이해는 기존 리스트를 기반으로 목록을 작성하기 위해 Cypher에서 사용할 수 있는 구문 구조입니다. 이것은 map 및 필터 함수를 사용하는 대신 수학적 설정-빌더(set comprehension) 표기법 형식을 따릅니다. 

**쿼리**

```
RETURN [x IN range(0,10) WHERE x % 2 = 0 | x^3] AS result
```

| result                              |
| ----------------------------------- |
| 1 row                               |
| `[0.0,8.0,64.0,216.0,512.0,1000.0]` |

필터 나 맵핑 만하고 싶다면```WHERE`` 부분이나 표현식을 생략 할 수 있습니다.
필터나 맵핑만하고 싶다면 ```WHERE``` 부분 또는 표현식을 생략할 수 있습니다. 

**쿼리**

```
RETURN [x IN range(0,10) WHERE x % 2 = 0] AS result
```

| result           |
| ---------------- |
| 1 row            |
| `[0,2,4,6,8,10]` |

**쿼리**

```
RETURN [x IN range(0,10)| x^3] AS result
```

| result                                                       |
| ------------------------------------------------------------ |
| 1 row                                                        |
| `[0.0,1.0,8.0,27.0,64.0,125.0,216.0,343.0,512.0,729.0,1000.0]` |

#### 3.2.10.3. 패턴 이해

패턴 이해는 패턴과 매칭되는 리스트를 생성할 때 Cypher에서 사용할 수 있는 구문입니다. 패턴 이해는 일반 ```MATCH```절과 같이 특정 패턴에 일치할 것이며, 술어는 일반적인 ```WHERE```절과 유사하지만 지정된 사용자 정의 투사를 산출합니다. 

다음 그래프는 아래 예와 같이 사용됩니다:

그림 3.4. Graph

![alt](https://neo4j.com/docs/developer-manual/3.3/images/Lists-1.svg)

**쿼리**

```
MATCH (a:Person { name: 'Charlie Sheen' })
RETURN [(a)-->(b) WHERE b:Movie | b.year] AS years
```

| years              |
| ------------------ |
| 1 row              |
| `[1979,1984,1987]` |

```WHERE``` 키워드를 포함한 모든 술어는 선택하거나 제거할 수 있습니다. 